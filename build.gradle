plugins {
    id 'java'
    id 'maven-publish'
}

group = project.maven_group
version = project.project_version

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/blackjack26/bgi-parser")  // GitHub Packages
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'bgi-parser'
            version = project.version

            pom {
                name = 'BGI Parser'
                description = 'A parser utility for the new BGI data structure'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'bnjc'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/blackjack26/bgi-parser")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME_GITHUB")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN_GITHUB")
            }
        }
    }
}